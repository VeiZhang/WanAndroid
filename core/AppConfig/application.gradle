def plugins = rootProject.ext.plugins
def cfg = rootProject.ext.android
def libs = rootProject.ext.dependencies

apply plugin: plugins.application
apply plugin: plugins.kotlin
apply plugin: plugins.safeargs

android {
    compileSdkVersion cfg.compileSdkVersion
    defaultConfig {
        applicationId cfg.applicationId
        minSdkVersion cfg.minSdkVersion
        targetSdkVersion cfg.targetSdkVersion
        versionCode cfg.versionCode
        versionName cfg.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // 兼容高版本Android Studio
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }

        multiDexEnabled true
        flavorDimensions "default"
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def versionName = cfg.versionName
            def flavorName = variant.flavorName
            outputFileName = "WanAndroid_${versionName}_${flavorName}.apk"
        }
    }

    dataBinding {
        enabled true
    }
    // 去掉AndroidManifest里面Google搜索的警告
    lintOptions {
        disable 'GoogleAppIndexingWarning'
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/*.kotlin_module'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation libs["core-ktx"]
}
